plugins {
	id 'org.springframework.boot' version '2.6.4'
	id 'io.spring.dependency-management' version '1.0.11.RELEASE'
	id 'java'
	id "com.github.node-gradle.node" version "3.1.0"
}

node {
	version = '14.16.1'
	npmVersion = '7.18.1'
	download = true
	workDir = file("${project.buildDir}/nodejs")
	npmWorkDir = file("${project.buildDir}/npm")
}

group = 'dare'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	runtimeOnly 'com.h2database:h2'
}

tasks.named('test') {
	useJUnitPlatform()
}

//이미 빌드된 파일 삭제
task deleteVueArtifact(type: Delete) {
	delete "src/main/resources/static"
}

//npm install
task frontNpmInstall(type: NpmTask) {
	workingDir = file("${project.projectDir}/src/front-end")
	args = ['install']
}

//npm run build
task npmBuild(type: NpmTask, dependsOn: ['deleteVueArtifact', 'frontNpmInstall']) {
	workingDir = file("${project.projectDir}/src/front-end")
	args = ['run', 'build']
}

processResources.dependsOn 'npmBuild'